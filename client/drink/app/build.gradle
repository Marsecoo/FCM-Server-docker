plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'//replace to  viewBinding
    id 'com.google.gms.google-services'
}
apply from: 'https://raw.githubusercontent.com/CKS-ROC/Useful_Gradle/master/version.gradle'
def version = this.ext.default_version()
project.version =version.versionName
ext {
    Debug_properties_file = "https://raw.githubusercontent.com/CMingTseng/Useful_Gradle/master/debug.properties"
    Release_properties_file = Debug_properties_file
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.raywenderlich.android.drinkit"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode version.versionCode.toInteger()
        versionName version.versionName
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "API_HOST", '"http://192.168.15.241:8890/"'
    }

    signingConfigs {
        Properties props = new Properties()
        debug {
            props.load(new URL(this.ext.Debug_properties_file).openStream())
            keyAlias "${props['alias']}"
            keyPassword "${props['key_password']}"
            storePassword "${props['keystore_password']}"
            storeFile file(buildDir.toString() + File.separator + "${props['keystore_file_name']}" + "${props['keystore_file_type']}")
        }

        release {
            props.load(new URL(this.ext.Release_properties_file).openStream())
            keyAlias "${props['alias']}"
            keyPassword "${props['key_password']}"
            storePassword "${props['keystore_password']}"
            storeFile file(buildDir.toString() + File.separator + "${props['keystore_file_name']}" + "${props['keystore_file_type']}")
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    // Kotlin Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0"

    implementation 'androidx.multidex:multidex:2.0.1'
    // Support Libraries
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'

    // Testing Dependencies
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // TUTORIAL DEPENDENCIES HERE
    implementation 'com.google.firebase:firebase-analytics:17.2.3'
    implementation 'com.google.firebase:firebase-messaging:20.1.3'

    implementation 'androidx.work:work-runtime:2.5.0'

    // Gson
    implementation "com.google.code.gson:gson:$gson_version"
    // OkHttp
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_interceptor_version"

    // Retrofit 2
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    // Timber
    implementation "com.jakewharton.timber:timber:$timber_version"

    implementation project(path: ':model_entity:fcm_token')

    implementation project(path: ':API_Module')
    implementation project(path: ':FCMManager')
    implementation project(path: ':FirebaseMessagingService')
}

task increaseVersion {
    group "version Tasks"
    doLast {
        this.project.ext.increaseVersion("")
    }
}

task increaseHotfixVersion {
    group "version Tasks"
    doLast {
        this.project.ext.increaseHotfixVersion("")
    }
}

task decreaseVersion {
    group "version Tasks"
    doLast {
        this.project.ext.decreaseVersion("")
    }
}

task showCurrentVersion {
    group "version Tasks"
    this.project.ext.currentVersion("")
}